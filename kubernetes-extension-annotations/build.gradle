/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

apply from: "$rootDir/gradle/mvnPublisher.gradle"

description = 'Ballerinax - Kubernetes Extension Annotations'

configurations {
    bDistributionDir
    balTools
    birDir
    kubernetesJar
    istioJar
    openshiftJar
    knativeJar
}

dependencies{
    balTools ("org.ballerinalang:jballerina-tools:${ballerinaLangVersion}"){
        transitive = false
    }
}

jar {
    enabled = false
}

clean {
    delete "$project.projectDir/target"
    delete "$project.projectDir/Ballerina.lock"
}

def bDistribution = file("$project.buildDir/extracted-distribution/jballerina-tools-${ballerinaLangVersion}")

task unzip {
    doLast {
        configurations.balTools.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File(project.buildDir, "extracted-distribution/")
            }
        }
    }
    outputs.dir bDistribution
}

def artifactBirDir = file("$project.projectDir/target/caches/bir_cache/")
def artifactKubernetesJar = file("$project.projectDir/target/caches/jar_cache/ballerina/kubernetes/ballerina.kubernetes.jar")
def artifactIstioJar = file("$project.projectDir/target/caches/jar_cache/ballerina/istio/ballerina.istio.jar")
def artifactOpenshiftJar = file("$project.projectDir/target/caches/jar_cache/ballerina/openshift/ballerina.openshift.jar")
def artifactKnativeJar = file("$project.projectDir/target/caches/jar_cache/ballerina/knative/ballerina.knative.jar")

task ballerinaBuild {
    dependsOn unzip
    inputs.dir file("$project.projectDir/src")
    inputs.file file("$project.projectDir/Ballerina.toml")
    doLast {
        exec {
            workingDir project.projectDir
            environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            def distributionBinPath = project.projectDir.absolutePath + "/build/extracted-distribution/jballerina-tools-${ballerinaLangVersion}/bin"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "$distributionBinPath/ballerina.bat build -c -a --skip-tests"
            } else {
                commandLine 'sh', '-c', "$distributionBinPath/ballerina build -c -a --skip-tests"
            }
        }
        // Rename kubernetes jar
        copy {
            from "$project.projectDir/target/caches/jar_cache/ballerina/kubernetes/ballerina-kubernetes-.jar"
            into "$project.projectDir/target/caches/jar_cache/ballerina/kubernetes/"
            rename { String fileName ->
                fileName.replace("ballerina-kubernetes-.jar", artifactKubernetesJar.getName())
            }
        }
        // Rename istio jar
        copy {
            from "$project.projectDir/target/caches/jar_cache/ballerina/istio/ballerina-istio-.jar"
            into "$project.projectDir/target/caches/jar_cache/ballerina/istio/"
            rename { String fileName ->
                fileName.replace("ballerina-istio-.jar", artifactIstioJar.getName())
            }
        }
        // Rename openshift jar
        copy {
            from "$project.projectDir/target/caches/jar_cache/ballerina/openshift/ballerina-openshift-.jar"
            into "$project.projectDir/target/caches/jar_cache/ballerina/openshift/"
            rename { String fileName ->
                fileName.replace("ballerina-openshift-.jar", artifactOpenshiftJar.getName())
            }
        }
        // Rename knative jar
        copy {
            from "$project.projectDir/target/caches/jar_cache/ballerina/knative/ballerina-knative-.jar"
            into "$project.projectDir/target/caches/jar_cache/ballerina/knative/"
            rename { String fileName ->
                fileName.replace("ballerina-knative-.jar", artifactKnativeJar.getName())
            }
        }
        // Copy the kubernetes BIR to the 0.0.0 folder within it
        ant.move file: "${artifactBirDir}/ballerina/kubernetes/kubernetes.bir",
                todir: "${artifactBirDir}/ballerina/kubernetes/0.0.0/"
        // Copy the istio BIR to the 0.0.0 folder within it
        ant.move file: "${artifactBirDir}/ballerina/istio/istio.bir",
                todir: "${artifactBirDir}/ballerina/istio/0.0.0/"
        // Copy the openshift BIR to the 0.0.0 folder within it
        ant.move file: "${artifactBirDir}/ballerina/openshift/openshift.bir",
                todir: "${artifactBirDir}/ballerina/openshift/0.0.0/"
        // Copy the knative BIR to the 0.0.0 folder within it
        ant.move file: "${artifactBirDir}/ballerina/knative/knative.bir",
                todir: "${artifactBirDir}/ballerina/knative/0.0.0/"

        // Copy the TOML to the same folder as the BIRs
        ["${artifactBirDir}/ballerina/kubernetes/0.0.0",
         "${artifactBirDir}/ballerina/istio/0.0.0",
         "${artifactBirDir}/ballerina/openshift/0.0.0",
         "${artifactBirDir}/ballerina/knative/0.0.0"].each { birDir ->
            copy {
                from file("$project.projectDir/Ballerina.toml")
                into birDir
            }
        }
    }
    outputs.dir artifactBirDir
    outputs.file artifactKubernetesJar
    outputs.file artifactIstioJar
    outputs.file artifactOpenshiftJar
    outputs.file artifactKnativeJar
}

artifacts {
    bDistributionDir file: bDistribution, builtBy: unzip
    birDir file: artifactBirDir, builtBy: ballerinaBuild
    kubernetesJar file: artifactKubernetesJar, builtBy: ballerinaBuild
    istioJar file: artifactIstioJar, builtBy: ballerinaBuild
    openshiftJar file: artifactOpenshiftJar, builtBy: ballerinaBuild
    knativeJar file: artifactKnativeJar, builtBy: ballerinaBuild
}

task createArtifactZip(type: Zip) {
    destinationDirectory = file("$buildDir/distributions")
    from ballerinaBuild
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }
}

build.dependsOn ballerinaBuild
